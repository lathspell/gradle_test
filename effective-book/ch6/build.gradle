plugins {
    id 'java'
    id 'application'
    id "com.dorongold.task-tree" version "1.3.1" // Usage: gradle assemble taskTree
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

test {
    testLogging.showStandardStreams = true          // print all stdout/stderr output to console
    testLogging.minGranularity = 0                  // show class and method names
    testLogging.events = ["passed", "skipped", "failed"]
}

// Start with custom generic task
task("run1") {
    dependsOn(classes)
    description = "Run App v1"

    doLast {
        javaexec {
            main = "ch6.App"
            classpath sourceSets.main.runtimeClasspath
            systemProperty "foo", "bar1"
            args = ["--type=1"]
        }
    }
}

// Start with custom task of type JavaExec
task run2(type: JavaExec) {
    dependsOn(classes)
    description = "Run App v2"

    main = "ch6.App"
    classpath sourceSets.main.runtimeClasspath
    systemProperty "foo", "bar2"
    args = ["--type=2"]
}

// Start using the "application" plugin
mainClassName = 'ch6.App'
run {
    systemProperties = ["foo": "bar3"]
    args = ["--type=3"]
}
